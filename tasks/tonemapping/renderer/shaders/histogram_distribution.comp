#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive : require

#include "tonemapping.h"


layout(local_size_x = 1) in;

layout(binding = 0, set = 0) buffer hist_t
{
  HistogramData data;
};

void main(void)
{
  data.minLuminance = normalized_to_luminance(data.minNormLuminance);
  data.maxLuminance = normalized_to_luminance(data.maxNormLuminance);

  data.minLogLuminance = to_logscale(data.minLuminance);
  data.maxLogLuminance = to_logscale(data.maxLuminance);

  data.binsCumDensity[0] = data.binsDensity[0];
  for (int i = 1; i < HISTOGRAM_BINS; ++i)
    data.binsCumDensity[i] = data.binsCumDensity[i - 1] + data.binsDensity[i];

  data.binsCumRefinedDensity[0] = data.binsRefinedDensity[0];
  for (int i = 1; i < HISTOGRAM_BINS; ++i)
  {
    data.binsCumRefinedDensity[i] =
      data.binsCumRefinedDensity[i - 1] + data.binsRefinedDensity[i];
  }

  data.binsDistibution[0] = 0.f;
  for (int i = 1; i < HISTOGRAM_BINS; ++i)
  {
    const float densityCoeff =
      float(data.binsCumDensity[i - 1]) /
      float(data.binsCumDensity[HISTOGRAM_BINS - 1]);
    const float refinedDensityCoeff =
      float(data.binsCumRefinedDensity[i - 1]) /
      float(data.binsCumRefinedDensity[HISTOGRAM_BINS - 1]);
    const float indexCoeff = float(i) / float(HISTOGRAM_BINS);

    data.binsDistibution[i] =
      0.4f * densityCoeff +
      0.4f * refinedDensityCoeff +
      0.2f * indexCoeff;
  }
}

